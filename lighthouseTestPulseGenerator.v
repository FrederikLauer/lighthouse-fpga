// Generates envelope and data signal to simulate a lighthouse V1 as received
// by a photodiode and generated by the analog part of TS4231
module lighthousePulseGenerator(
  output reg ir_e,
  output ir_d
);

  // Generating IR signals.
  // Simulates (quickly!) a Lighthouse V1 with 2MHz carrier
  reg ir_carrier = 0;
  reg ir_data_received = 0;
  assign ir_d = (ir_data_received)?ir_carrier:1'b0;

  task generate_ir_pulse;
    input time length;
    fork : f
      begin
        // Continious carrier, stopped by the pulse generation
        repeat(10000) #166ns ir_carrier = (ir_carrier === 1'b0);
      end
      begin
        ir_data_received = 1;
        repeat (4) @(posedge ir_carrier);
        ir_e = 0;
        #length;
        ir_data_received = 0;
        repeat (2) @(posedge ir_carrier);
        ir_e = 1;
        disable f;
      end
    join
  endtask

  always begin
    #100us;
    generate_ir_pulse(20us);
    #100us;
    generate_ir_pulse(30us);
    #300us;
    generate_ir_pulse(3us);
    #10us;
  end

  initial ir_e = 1;

endmodule // lighthousePulseGenerator